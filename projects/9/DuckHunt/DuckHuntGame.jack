class DuckHuntGame {
    field Bird bird;
    field Crosshair crosshair;
    field int score;
    field int lives;
    field int state; 
    field String sTitle, sEnter, sQuit, sGameOver, sScore, sLives, sFinalScore, sRestart;

    constructor DuckHuntGame new() {
        let sTitle = "DUCKHUNT";
        let sEnter = "PRESS ENTER";
        let sQuit = "Q TO QUIT";
        let sGameOver = "GAME OVER";
        let sScore = "SCORE: ";
        let sLives = "LIVES: ";
        let sFinalScore = "FINAL SCORE: ";
        let sRestart = "PRESS ENTER TO RESTART";
        
        let state = 0;
        let score = 0;
        let lives = 3;
        
        let bird = Bird.new();
        let crosshair = Crosshair.new(256, 128);
        return this;
    }

    method void dispose() {
        do bird.dispose();
        do crosshair.dispose();
        do sTitle.dispose();
        do sEnter.dispose();
        do sQuit.dispose();
        do sGameOver.dispose();
        do sScore.dispose();
        do sLives.dispose();
        do sFinalScore.dispose();
        do sRestart.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void drawMenu() {
        do Screen.clearScreen();
        do Screen.setColor(true);
        do Screen.drawRectangle(0, 0, 511, 5);
        do Screen.drawRectangle(0, 250, 511, 255);
        do Screen.drawRectangle(0, 0, 5, 255);
        do Screen.drawRectangle(506, 0, 511, 255);
        
        do Screen.drawCircle(100, 60, 20);
        do Screen.drawCircle(120, 60, 25);
        do Screen.drawCircle(140, 60, 20);
        
        do Screen.drawCircle(400, 80, 15);
        do Screen.drawCircle(415, 80, 20);
        do Screen.drawCircle(430, 80, 15);

        do Output.moveCursor(10, 27);
        do Output.printString(sTitle);
        do Output.moveCursor(14, 26);
        do Output.printString(sEnter);
        do Output.moveCursor(16, 27);
        do Output.printString(sQuit);
        return;
    }

    method void drawUI() {
        do Output.moveCursor(0, 1);
        do Output.printString(sLives);
        do Output.printInt(lives);
        do Output.moveCursor(0, 50);
        do Output.printString(sScore);
        do Output.printInt(score);
        return;
    }

    method void drawShotgun() {
        do Screen.setColor(true);
        do Screen.drawRectangle(240, 220, 272, 255);
        do Screen.drawRectangle(250, 190, 262, 220);
        return;
    }

    method void run() {
        var char key;
        var boolean exit;
        let exit = false;

        while (~exit) {
            if (state = 0) {
                do drawMenu();
                while (state = 0) {
                    let key = Keyboard.keyPressed();
                    if (key = 128) { let state = 1; do Screen.clearScreen(); }
                    if (key = 81) { let state = 3; let exit = true; }
                    do Sys.wait(50);
                }
            }

            if (state = 1) {
                let score = 0;
                let lives = 3;
                do bird.reset();
                while (state = 1) {
                    let key = Keyboard.keyPressed();
                    if (key = 81) { let state = 0; }
                    
                    if (key = 131) { do crosshair.move(0, -5); }
                    if (key = 133) { do crosshair.move(0, 5); }
                    if (key = 130) { do crosshair.move(-5, 0); }
                    if (key = 132) { do crosshair.move(5, 0); }
                    
                    if (key = 32) {
                        if (bird.checkHit(crosshair.getX(), crosshair.getY())) {
                            let score = score + 1;
                            do bird.reset();
                        }
                    }

                    do bird.update();
                    if (bird.isOut()) {
                        let lives = lives - 1;
                        do bird.reset();
                    }

                    if (lives < 1) {
                        let state = 2;
                    }

                    do Screen.setColor(false);
                    do Screen.drawRectangle(0, 0, 511, 12);
                    do drawUI();
                    do bird.draw();
                    do crosshair.draw();
                    do drawShotgun();
                    
                    do Sys.wait(30);
                    do bird.erase();
                    do crosshair.erase();
                }
            }

            if (state = 2) {
                do Screen.clearScreen();
                do Output.moveCursor(10, 27);
                do Output.printString(sGameOver);
                do Output.moveCursor(12, 25);
                do Output.printString(sFinalScore);
                do Output.printInt(score);
                do Output.moveCursor(16, 20);
                do Output.printString(sRestart);
                
                while (state = 2) {
                    let key = Keyboard.keyPressed();
                    if (key = 128) { let state = 0; }
                    if (key = 81) { let state = 3; let exit = true; }
                    do Sys.wait(50);
                }
            }
        }
        return;
    }
}