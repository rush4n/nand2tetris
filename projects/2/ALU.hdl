// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //Step 1: zero or not
    Mux16(a=x, b[0..15]=false, sel=zx, out=zxOut);
    Mux16(a=y, b[0..15]=false, sel=zy, out=zyOut);

    //Step 2: negate(x, y) or not
    Not16(in=zxOut, out=notX);
    Mux16(a=zxOut, b=notX, sel=nx, out=nxOut);
    Not16(in=zyOut, out=notY);
    Mux16(a=zyOut, b=notY, sel=ny, out=nyOut);

    //Step 3: Add or And
    And16(a=nxOut, b=nyOut, out=andOut);
    Add16(a=nxOut, b=nyOut, out=addOut);
    Mux16(a=andOut, b=addOut, sel=f, out=fOut);

    //Step 4: negate(out) or not
    Not16(in=fOut, out=noOut);
    // if MSB 1 then ng=1 else ng=0 -> i.e ng=MSB
    Mux16(a=fOut, b=noOut, sel=no, out=out, out[15]=ng, out[0..7]=zrLow, out[8..15]=zrHigh);

    //Step 5: set zr flag
    Or8Way(in=zrLow, out=lowOut);
    Or8Way(in=zrHigh, out=highOut);
    Or(a=lowOut, b=highOut, out=notZr);
    Not(in=notZr, out=zr);
}